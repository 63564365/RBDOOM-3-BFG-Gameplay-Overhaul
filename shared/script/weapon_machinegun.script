/***********************************************************************

weapon_machinegun.script

***********************************************************************/

#define MACHINEGUN_FIRERATE			0.05
#define MACHINEGUN_LOWAMMO			10
#define MACHINEGUN_NUMPROJECTILES	1

// blend times
#define MACHINEGUN_IDLE_TO_LOWER	4
#define MACHINEGUN_IDLE_TO_FIRE		0
#define	MACHINEGUN_IDLE_TO_RELOAD	4
#define MACHINEGUN_RAISE_TO_IDLE	4
#define MACHINEGUN_FIRE_TO_IDLE		0
#define MACHINEGUN_RELOAD_TO_IDLE	4

#define MACHINEGUN_BEST_SHOTS		3
#define MACHINEGUN_BEST_SPREAD		0.5
#define MACHINEGUN_NORMAL_SRREAD	1.5

object weapon_machinegun : weapon_base {
	float		next_attack;
	float		accurate_shots;
	
	void		init();
	
	void		Lower();
	void		Raise();
	void		Idle();
	void		Fire();
	void		Reload();
	void		ExitCinematic();
};

void weapon_machinegun::init() {
	next_attack = 0;
	accurate_shots = MACHINEGUN_BEST_SHOTS;
	weaponState( "Raise", 0 );
}

void weapon_machinegun::Raise() {
	weaponRising();
	playAnim( ANIMCHANNEL_ALL, "raise" );
	waitUntil( animDone( ANIMCHANNEL_ALL, MACHINEGUN_RAISE_TO_IDLE ) );
	weaponState( "Idle", MACHINEGUN_RAISE_TO_IDLE );
}

void weapon_machinegun::Lower() {
	weaponLowering();
	playAnim( ANIMCHANNEL_ALL, "putaway" );
	waitUntil( animDone( ANIMCHANNEL_ALL, 0 ) );
	weaponHolstered();
	waitUntil( WEAPON_RAISEWEAPON );
	weaponState( "Raise", 0 );
}

void weapon_machinegun::Idle() {
	if ( !ammoAvailable() ) {
		weaponOutOfAmmo();
	} else {
		weaponReady();
	}

	playCycle( ANIMCHANNEL_ALL, "idle" );
	while( 1 ) {
		if ( WEAPON_LOWERWEAPON ) {
			weaponState( "Lower", MACHINEGUN_IDLE_TO_LOWER );
		}
		if ( WEAPON_ATTACK ) {
			if ( ( sys.getTime() >= next_attack ) && ( ammoAvailable() > 0 ) ) {
				weaponState( "Fire", MACHINEGUN_IDLE_TO_FIRE );
			}
		} else {
			accurate_shots = MACHINEGUN_BEST_SHOTS;
		}
		waitFrame();
	}
}

void weapon_machinegun::Fire() {
	float spread;

	next_attack = sys.getTime() + MACHINEGUN_FIRERATE;
	
	if ( accurate_shots > 0 ) {
		spread = MACHINEGUN_BEST_SPREAD;
		accurate_shots = accurate_shots - 1;
	} else {
		spread = MACHINEGUN_NORMAL_SRREAD;
	}

	if ( ammoAvailable() == MACHINEGUN_LOWAMMO ) {
		startSound( "snd_lowammo", SND_CHANNEL_ITEM, true );
	}

	launchProjectiles( MACHINEGUN_NUMPROJECTILES, spread, 0, 1.0, 1.0 );
	playAnim( ANIMCHANNEL_ALL, "fire" );
	while( !animDone( ANIMCHANNEL_ALL, MACHINEGUN_FIRE_TO_IDLE ) ) {
		waitFrame();
	}

	weaponState( "Idle", MACHINEGUN_FIRE_TO_IDLE );
}

void weapon_machinegun::Reload() {
	weaponState( "Idle", MACHINEGUN_RELOAD_TO_IDLE );
}

void weapon_machinegun::ExitCinematic() {
	next_attack = 0;
	weaponState( "Idle", 0 );
}
